ACTOR FireballSpitter : SwitchableDecoration 12700
{
	//$Title Fireball Spitter
	//$Category Traps
	//$IsDecoration
	//Angled
	//$Sprite FARGD0
	//$Arg0 Shot Delay (seconds)
	//$Arg0Default 3
	//$Arg1 Projectile Speed
	//$Arg1Default 10
	
	Var int user_shotdelay;
	Var int user_shotspeed;

	+NOCLIP
	+NOGRAVITY
	+FIXMAPTHINGPOS
	//+WALLSPRITE
	-RANDOMIZE
	Radius 1
	Height 40
	
		States
		{
			Spawn:
				FARG A 0 NoDelay A_SetUserVar("user_shotdelay", Args[0])
			ShotDelay:
				FARG A 35 A_SetUserVar("user_shotdelay", user_shotdelay-1)
				FARG A 0 A_JumpIf(user_shotdelay==0,"Active")
				Loop
			Active:
				FARG B 4
				FARG C 8
				FARG D 8 Bright A_SpawnItemEx ("SpitterShot", 1, 0, 0, Args[1], 0, 0, 0, 0, 0, 0)
				FARG C 8
				FARG B 4
				Goto Spawn
			Inactive:
				FARG A -1 A_SetUserVar("user_shotdelay", Args[0])
				Stop
		}
}


ACTOR FireballSpitterInactive : FireballSpitter 12701
{
	//$Title Fireball Spitter Inactive
	//$Sprite FARGA0

		States
		{
			Spawn:
				Goto Super::Inactive
		}
}

ACTOR FlameJetter : SwitchableDecoration 12702
{
	//$Title Flame Jetter
	//$Category Traps
	//$IsDecoration
	//Angled
	//$Sprite FARGD0
	//$Arg0 Jet Delay (seconds)
	//$Arg0Default 3
	//$Arg1 Jet Duration (seconds)
	//$Arg1Default 3
	
	Var int user_jetdelay;
	Var int user_jetduration;

	+NOCLIP
	+NOGRAVITY
	+FIXMAPTHINGPOS
	//+WALLSPRITE
	-RANDOMIZE
	Radius 1
	Height 40
		
		States
		{
			Spawn:
				FARG A 0 NoDelay A_SetUserVar("user_jetdelay", Args[0])
				FARG A 0 A_SetUserVar("user_jetduration", Args[1])
			ShotDelay:
				FARG A 35 A_SetUserVar("user_jetdelay", user_jetdelay-1)
				FARG A 0 A_JumpIf(user_jetdelay==0,"Active")
				Loop
			Active:
				FARG B 4
				FARG C 8
				FARG D 0 A_PlaySound ("FireballSpitter/Launch")
				FARG D 0 A_PlaySound("FlameJetter/Fire", CHAN_6, 1, TRUE)
			FlameJet:
				FARG D 0 Bright A_SetUserVar("user_jetduration", user_jetduration-1)
				FARG DDDDDDD 5 Bright A_SpawnItemEx ("JetFlameShot", 1, 0, 0, 160, 0, 0, 0, 0, 0)
				FARG D 0 A_JumpIf(user_jetduration==0,"StopJet")
				Loop
			StopJet:
				FARG C 0 A_StopSound(CHAN_6)
				FARG C 8 A_PlaySound ("FireballSpitter/Hit")
				FARG B 4
				Goto Spawn
			Inactive:
				FARG A 0 A_SetUserVar("user_jetdelay", Args[0])
				FARG A -1 A_SetUserVar("user_jetduration", Args[1])
				Stop
		}
}

ACTOR FlameJetterInactive : FlameJetter 12703
{
	//$Title Flame Jetter Inactive
	//$Sprite FARGA0

		States
		{
			Spawn:
				Goto Super::Inactive
		}
}

ACTOR FlameJetterToggle : SwitchableDecoration 12706
{
	//$Title Flame Jetter Toggle
	//$Category Traps
	//$IsDecoration
	//Angled
	//$Sprite FARGA0

	+NOCLIP
	+NOGRAVITY
	+FIXMAPTHINGPOS
	//+WALLSPRITE
	-RANDOMIZE
	Radius 1
	Height 40
		
		States
		{
			Inactive:
				FARG C 0 A_StopSound(CHAN_6)
				FARG C 8 A_PlaySound ("FireballSpitter/Hit")
				FARG B 4
			Spawn:
				FARG A -1
				Stop
			Active:
				FARG B 4
				FARG C 8
				FARG D 0 A_PlaySound ("FireballSpitter/Launch")
				FARG D 0 A_PlaySound("FlameJetter/Fire", CHAN_6, 1, TRUE)
			FlameJet:
				FARG D 5 Bright A_SpawnItemEx ("JetFlameShot", 1, 0, 0, 160, 0, 0, 0, 0, 0)
				Loop
		}
}

ACTOR FloorGrill : SwitchableDecoration 12704
{
	//$Title Flaming Floor Grill
	//$Category Traps
	//$IsDecoration
	//$Sprite GRILB0
	//$Icon GRILB0
	//$NotAngled
	//$Arg0 Jet Delay (seconds)
	//$Arg0Default 3
	//$Arg1 Jet Duration (seconds)
	//$Arg1Default 3
	
	Var int user_jetdelay;
	Var int user_jetduration;

	-RANDOMIZE
	Radius 20
	Height 1
		
		States
		{
			Spawn:
				GRIL A 0 NoDelay A_SetUserVar("user_jetdelay", Args[0])
				GRIL A 0 A_SetUserVar("user_jetduration", Args[1])
			ShotDelay:
				GRIL A 35 A_SetUserVar("user_jetdelay", user_jetdelay-1)
				GRIL A 0 A_JumpIf(user_jetdelay==0,"Active")
				Loop
			Active:
				GRIL A 0 A_PlaySound ("FireballSpitter/Launch")
				GRIL A 0 A_PlaySound("FlameJetter/Fire", CHAN_6, 1, TRUE)
			FlameJet:
				GRIL B 0 Bright A_SetUserVar("user_jetduration", user_jetduration-1)
				GRIL BBBBBBB 5 Bright A_SpawnItemEx ("FloorFlameSpawner", 0, 0, 0, 0, 0, 4, 0, 0, 0)
				GRIL B 0 Bright A_JumpIf(user_jetduration==0,"StopJet")
				Loop
			StopJet:
				GRIL A 0 A_StopSound(CHAN_6)
				GRIL A 0 A_PlaySound ("FireballSpitter/Hit")
				GRIL A 0
				Goto Spawn
			Inactive:
				GRIL A 0 A_SetUserVar("user_jetdelay", Args[0])
				GRIL A -1 A_SetUserVar("user_jetduration", Args[1])
				Stop
		}
}

ACTOR FloorGrillInactive : FloorGrill 12705
{
	//$Title Flaming Floor Grill Inactive

		States
		{
			Spawn:
				Goto Super::Inactive
		}
}

ACTOR FloorGrillToggle : SwitchableDecoration 12707
{
	//$Title Flaming Floor Grill Toggle
	//$Category Traps
	//$IsDecoration
	//$Sprite GRILA0
	//$Icon GRILA0
	//$NotAngled

	-RANDOMIZE
	Radius 20
	Height 1
			
		States
		{
			Inactive:
				GRIL A 0 A_StopSound(CHAN_6)
				GRIL A 0 A_PlaySound ("FireballSpitter/Hit")
			Spawn:
				GRIL A -1
				Stop
			Active:
				GRIL A 0 A_PlaySound ("FireballSpitter/Launch")
				GRIL A 0 A_PlaySound("FlameJetter/Fire", CHAN_6, 1, TRUE)
			FlameJet:
				GRIL B 5 Bright A_SpawnItemEx ("FloorFlameSpawner", 0, 0, 0, 0, 0, 4, 0, 0, 0)
				Loop

		}
}



ACTOR SpitterShot
{
	Radius 4
	Height 4
	Speed 10
	Damage 4
	DamageType Fire
	RenderStyle Add
	DeathSound "FireballSpitter/Hit"
	Decal "SpitterScorch"
	Projectile 
	+ROCKETTRAIL
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	
		States
		{
			Spawn:
				FARF A 0 Bright NoDelay A_PlaySound("FireballSpitter/Flight", CHAN_6, 1, TRUE)
				FARF A 0 Bright A_PlaySound ("FireballSpitter/Launch")
			Fly:
				FARF AB 3 Bright
				Loop
			Death:
				BAL1 CDE 5 Bright A_StopSound(CHAN_6)
				Stop
		}
}

ACTOR JetFlameShot : FastProjectile
{
	Radius 4
	Height 4
	Speed 22
	Damage 0
	ReactionTime 2
	RenderStyle Add
	MissileType "JetFlameSpawner"
	Projectile 
	+RIPPER
	+BLOODLESSIMPACT
	-ACTIVATEIMPACT
	-ACTIVATEPCROSS
	
		States
		{
			Spawn:
				TNT1 A 1 NoDelay A_Countdown
				Loop
		}
}

ACTOR JetFlame
{
	RenderStyle "Add"
	Alpha 0.8
	+NOINTERACTION
	
		States
		{
			Spawn:
				GFIR ABCDEFG 2 Bright NoDelay 
				Stop
		}
}

ACTOR JetFlameSpawner
{
	+NOINTERACTION
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+FORCERADIUSDMG
	DamageType Fire
	
		States
		{
			Spawn:
				TNT1 A 0 NoDelay A_SpawnItemeX ("JetFlame", 0, 0, 4, 0, random(0, 1), 1, random (1, 360))
				TNT1 A 16 A_Explode (4, 16)
				Stop
		}
}

ACTOR FloorFlameSpawner
{
	+NOINTERACTION
	+NODAMAGETHRUST
	+BLOODLESSIMPACT
	+FORCERADIUSDMG
	DamageType Fire
	
		States
		{
			Spawn:
				TNT1 A 0 NoDelay A_SpawnItemeX ("JetFlame", random(-12, 12), 0, -4, 0, 0, 1, random (1, 360))
				TNT1 A 16 A_Explode (5, 24)
				Stop
		}
}